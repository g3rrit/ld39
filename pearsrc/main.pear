include"pstd"
include"SDL2/SDL"
include"objectmanager"
include"gamestate"
include"scenemanager"
local include"gfx"
local include"input"

main : () -> int 
{
    window.init();
    sceneManager.init();
    gameContainer.start();

    event.delete();

    return 0;
}

GameContainer : struct
{
    start : () -> void 
    {
        tex : SDL_Texture* = loadTexture("/../res/char/Sprite.bmp");

        //main loop
        sdlevent : SDL_Event;
        quit : bool = false;
 
        //TODO check if this works
        currentTime : struct timeval;
        gettimeofday(&currentTime,null);
 
        lastTime : f64 = (currentTime.tv_sec * 1000000 + currentTime.tv_usec) / 1000000.0;
        firstTime : f64 = 0;
        passedTime : f32 = 0;
        unprocessedTime : f32 = 0;
        frameTime : f32 = 0;
        frames : int = 0;
        render : bool = true;
 
        frameCap : f32 = 1.0 / 30.0;
        FPS : int = 0;
 
        while(!quit)
        {
            
            while(SDL_PollEvent(&sdlevent))
            {
                if(sdlevent.type == SDL_QUIT)
                {
                    quit = true;
                }
                else if(sdlevent.type == SDL_KEYDOWN)
                {
                }
                else if(sdlevent.type == SDL_MOUSEBUTTONDOWN)
                {
                }
            } 


            
            gettimeofday(&currentTime,null);
            firstTime = (currentTime.tv_sec * 1000000 + currentTime.tv_usec) / 1000000.0;
            //printf("time = %f \n", firstTime);
            passedTime = firstTime - lastTime;
            lastTime = firstTime;
    
            unprocessedTime = unprocessedTime + (f32)passedTime;
            frameTime = frameTime + passedTime;
  
            //printf("unprocessed : %f , framecap : %f fps : %i \n", unprocessedTime, frameCap, FPS);
 
            while(unprocessedTime >= frameCap)
            {
                gameContainer.update((f32)frameCap);
                unprocessedTime = unprocessedTime - frameCap;
                render = true;
 
                if(frameTime >= 1)
                {
                    printf("FPS: %i \n", FPS);
                    frameTime = 0;
                    FPS = frames;
                    frames = 0;
                }
            }
 
            if(render)
            {
                //render the scene
                SDL_RenderClear(window.ren);
                //render stuff
                gameContainer.draw();
                //SDL_RenderCopy(window.ren, tex, null,null);
                //
                SDL_RenderPresent(window.ren);
 
                frames ++;
                render = false;
            }
            else
            {
                usleep(10000);
            }

        }
 
        SDL_DestroyTexture(tex);
        gameContainer.stop();
    }

    stop : () -> void
    {
        event.dispatch("stop", null);
    }

    draw : () -> void
    {
        sceneManager.draw();
        event.dispatch("draw", null);
    }
    
    update : (dt : f32) -> void
    {
        input.update(dt);
        gameState.dt = dt;
        sceneManager.update(dt);
        event.dispatch("update",null);
    }
        
} : gameContainer

Window : struct
{
    win : SDL_Window* = null;
    ren : SDL_Renderer* = null;

    init : () -> bool
    {
        if(SDL_Init(SDL_INIT_VIDEO) != 0)
        {
            printf("error initing sdl\n");
        }
        printf("sdl inited\n");

        sdlwindow : SDL_Window* = SDL_CreateWindow("Explore", 100,100,640, 480, SDL_WINDOW_SHOWN);
        if(sdlwindow == null)
        {
            printf("Error creating window");
            SDL_Quit();
            return false;
        }
        printf("SDL window created \n");
  
        sdlren : SDL_Renderer* = SDL_CreateRenderer(sdlwindow,-1, SDL_RENDERER_ACCELERATED); //| SDL_RENDERER_PRESENTVSYNC);
        if(sdlren == null)
        {
            printf("Error creating renderer");
            SDL_DestroyWindow(sdlwindow);
            SDL_Quit();
 
            return false;
        }
        printf("sdl renderer created \n");
    
        window.win = sdlwindow;
        window.ren = sdlren;

        return true;
    }

    delete : () -> void
    {
        SDL_DestroyWindow(window.win);
        SDL_DestroyRenderer(window.ren);
    }

    
    
} : window


